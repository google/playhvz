// Uses Firebase Bolt to generate firebase rules JSON.
// https://github.com/firebase/bolt/blob/master/docs/guide.md
// To generate the rules json:
//    firebase-bolt rules.bolt
// Then, paste it into firebase's rules console.

// #1 patch around https://github.com/firebase/bolt/issues/95
// #2 patch around https://github.com/firebase/bolt/issues/58

// The following mimics the contents of sample_firebase.json

/******************************************************************************
 Define IDs - Simple Strings
******************************************************************************/
type ChatRoomId extends String { validate() { this.startsWith("chatRoom-") } }
type GameId extends String { validate() { this.startsWith("game-") } }
type OptionalGameId extends String { validate() { this == "" || this.startsWith("game-") } }
type MessageId extends String { validate() { this.startsWith("message-") } }
type PlayerId extends String { validate() { this.startsWith("player-") } }
type OptionalPlayerId extends String { validate() { this == "" || this.startsWith("player-") } }
type GroupId extends String { validate() { this.startsWith("group-") } }
type UserId extends String { validate() { !this.includes("-") } }
type MissionId extends String { validate() { this.startsWith("mission-") } }
type RewardCategoryId extends String { validate() { this.startsWith("rewardCategory-") } }
type GunId extends String { validate() { this.startsWith("gun-") } }
type RewardId extends String { validate() { this.startsWith("reward-") } }
type LifeId extends String { }

/******************************************************************************
 Simple Types That Need Validation
******************************************************************************/
type Allegiance extends String {
  validate() {
    this == 'resistance' || this == 'horde'
  }
}

type OptionalAllegiance extends String {
  validate() {
    this == 'none' || this == 'resistance' || this == 'horde'
  }
}

type Timestamp extends Number {
  validate() {
    this > 1490000000000 // March 2017, milliseconds
  }
}

type OptionalTimestamp extends Number {
  validate() {
    this == 0 || this > 1490000000000 // March 2017, milliseconds
  }
}

type HtmlUrl extends String {
  validate() {
    this.includes("/") && this.includes(".")
  }
}

/******************************************************************************
 Simple Types
******************************************************************************/
type ImageUrl {
  url: HtmlUrl;
}

type Location {
  latitude: Number;
  longitude: Number;
}

/******************************************************************************
 Helper Functions Needed By Complex Types
******************************************************************************/

getUserId() {
  ('user-' + auth.uid)
}

getCurrentUser() {
  root.users[getUserId()]
}

getCurrentPlayerId(gameId) {
  getCurrentUser().games[gameId].playerId
}

isGroupMember(groupId, gameId) {  
  root.groups[groupId].players[getCurrentPlayerId(gameId)]
}

isAdmin(gameId) {
  root.games[gameId].adminsUsers[getUserId()]
}

isAuthenticated() {
  auth != null && auth.uid != null
}

isServer() {
  auth.uid == 'ZiD9uLhDnrLq2n416MjWjn0JOrci6H0oGm7bKyVN' // TODO: place in safe
}

isAuthenticatedMember(groupId, gameId) {
  isAuthenticated() && (
  isServer() ||
  isAdmin(gameId) || 
  isGroupMember(groupId, gameId))
}

/******************************************************************************
 Complex Types
******************************************************************************/

type ChatRoom {
  ChatRoomId: ChatRoomId; // #1
  name: String;
  withAdmins: Boolean;
  gameId: GameId;
  groupId: GroupId;
  messages: Map<MessageId, Message>;

  read() {
    isAuthenticatedMember(this.groupId, this.gameId)
  }

  write() {     isServer()   }

  validate() {
    root.games[this.gameId] &&
    root.groups[this.groupId]
  }
}


type Message {
  MessageId: MessageId; // #1
  message: String;
  playerId: PlayerId;
  time: Timestamp;
  image: ImageUrl;
  location: Location;

  read() {
    isAuthenticatedMember(this.parent().groupId, this.parent().gameId)
  }

  write() {     isServer()   }

  validate() {
    root.chatRooms[this.parent().ChatRoomId]
  }
}

type Game {
  GameId: GameId; // #1
  active: Boolean;
  name: String;
  startTime: OptionalTimestamp;
  endTime: OptionalTimestamp
  registrationEndTime: OptionalTimestamp;
  stunTimer: Number;
  faqHtml: String;
  rulesHtml: String;

  adminUsers: Map<UserId, Boolean>;
  chatRooms: Map<ChatRoomId, Boolean>;
  missions: Map<MissionId, Boolean>;
  guns: Map<GunId, Boolean>;
  players: Map<PlayerId, Boolean>;
  rewardCategories: Map<RewardCategoryId, Boolean>;

  write() {     isServer()   }
  
  validate() {
    key() == this.GameId
  }
}

type Group {
  GroupId: GroupId; // #1
  allegianceFilter: OptionalAllegiance;
  autoAdd: Boolean;
  autoRemove: Boolean;
  canAddOthers: Boolean;
  canAddSelf: Boolean;
  canRemoveOthers: Boolean;
  canRemoveSelf: Boolean;
  gameId: GameId;
  name: String;
  ownerPlayerId: PlayerId;
  players: Map<PlayerId, Boolean>;

  read() {
    isAuthenticatedMember(this.GroupId, this.gameId)
  }
  
  write() {     isServer()   }

  validate() {
    root.games[this.gameId] != null
  }
}


type Gun {
  GunId: GunId; // #1
  playerId: OptionalPlayerId;
  gameId: OptionalGameId;
  label: String;

  read() {
    isAuthenticated()
  }  

  write() {     isServer()   }

  validate() {
    this.label.length > 0
  }
}

type Mission {
  MissionId: MissionId; // #1
  beginTime: Timestamp;
  endTime: Timestamp;
  detailsHtml: String;
  gameId: GameId;
  groupId: GroupId;
  name: String;
  rsvpersGroupId: GroupId;

  read() {
    isAuthenticatedMember(this.groupId, this.gameId)
  }

  write() {     isServer()   }

  validate() {
    root.games[this.gameId] != null
  }
}

type Volunteer {
  admin: Boolean;
  advertising: Boolean;
  android: Boolean;
  chronicler: Boolean;
  cleric: Boolean;
  client: Boolean;
  communications: Boolean;
  ios: Boolean;
  logistics: Boolean;
  moderator: Boolean;
  photographer: Boolean;
  server: Boolean;
  sorcerer: Boolean;
}

type NotificationSettings {
  sound: Boolean;
  vibrate: Boolean;
}

type PrivatePlayer extends PlayerBase {
  beInPhotos: Boolean;
  canInfect: Boolean;
  gameId: GameId;
  gotEquipment: Boolean;
  needGun: Boolean;
  notes: String;
  notificationSettings: NotificationSettings;
  userId: UserId;
  volunteer: Volunteer;
  wantToBeSecretZombie: Boolean;
  chatRooms: Map<ChatRoomId, Boolean>;
  missions: Map<MissionId, Boolean>;
  lives: Map<LifeId, PrivateLife>

  read() {
    isAuthenticated() && (isServer() ||
    getUserId() == this.userId)
  }

  write() {     isServer()   }

  validate() {
    root.users[this.userId] != null
  }
}


type Reward {
  RewardId: RewardId; // #1
  rewardCategoryId: RewardCategoryId;
  time: Timestamp;
  code: String;
  playerId: OptionalPlayerId;

  read() {
    isAuthenticated()
  }

  write() {     isServer()   }

  validate() { // #2
    this.playerId == "" ||
    root.publicPlayers[this.playerId] != null
  }
}

type PublicPlayer extends PlayerBase {
  active: Boolean;
  allegiance: Allegiance;
  claims: Map<RewardId, Reward>;
  name: String;
  number: Number;
  points: Number;
  profileImageUrl: HtmlUrl;
  userId: UserId;
  lives: Map<LifeId, PublicLife>

  read() {
    isAuthenticated() && (isServer() ||
    getUserId() == this.userId)
  }

  write() {     isServer()   }

  validate() {
    root.users[this.userId] != null
  }
}

type RewardCategory {
  RewardCategoryId: RewardCategoryId; // #1
  badgeImageUrl: HtmlUrl;
  claimed: Number;
  description: String;
  gameId: GameId;
  limitPerPlayer: Number;
  name: String;
  points: Number;
  rewards: Map<RewardId, Boolean>;
  shortName: String;

  read() {
    isAuthenticated()
  }

  write() {     isServer()   }

  validate() {
    root.games[this.gameId] != null
  }
  
}

type Player {
  gameId: GameId;
}

type PlayerBase {
  PlayerId: PlayerId; // #1
}

type LifeBase {
  LifeId: LifeId; // #1
}

type GamePlayerLookup {
  playerId: PlayerId;
}

type User {
  UserId: UserId; // #1
  a: Boolean;
  players: Map<PlayerId, Player>;
  games: Map<GameId, GamePlayerLookup>;

  read() {
    isServer() ||
    key() == getUserId()
  }

  write() {     isServer()   }

  validate() {
    true;
  }
}

type PrivateLife extends LifeBase {
  code: String;
}

type PublicLife extends LifeBase {
  time: OptionalTimestamp;
}

/******************************************************************************
 Possible Paths
******************************************************************************/
/ {
  write() { isServer() }
}

/chatRooms/{ChatRoomId} is ChatRoom;
/chatRooms/ChatRoomId/messages/{MessageId} is Message;
/games/{GameId} is Game;
/games {
  // Everyone can read a game so they can add themselves
  read() {
    isAuthenticated() 
  }
}
/games/GameId/adminUsers/{UserId} is User;
/groups/{GroupId} is Group;
/groups/GroupId/players/{PlayerId} is PrivatePlayer;
/guns/{GunId} is Gun;
/missions/{MissionId} is Mission;
/playersPrivate/{PlayerId} is PrivatePlayer;
/playersPrivate/PlayerId/lives/{LifeId} is PrivateLife;
/playersPublic/{PlayerId} is PublicPlayer;
/playersPublic/PlayerId/lives/{LifeId} is PublicLife;
/rewardCategories/{RewardCategoryId} is RewardCategory;
/rewards/{RewardId} is RewardCategory;
/users/{UserId} is User;
/users/UserId/players/{PlayerId} is PrivatePlayer;
