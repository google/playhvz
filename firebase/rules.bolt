// Uses Firebase Bolt to generate firebase rules JSON.
// https://github.com/firebase/bolt/blob/master/docs/guide.md
// To generate the rules json:
//    firebase-bolt rules.bolt
// Then, paste it into firebase's rules console.

// #1 patch around https://github.com/firebase/bolt/issues/95
// #2 patch around https://github.com/firebase/bolt/issues/58

// The following mimics the contents of sample_firebase.json

/******************************************************************************
 Define IDs - Simple Strings
******************************************************************************/
type ChatRoomId extends String { validate() { this.startsWith("chatRoom-") } }
type GameId extends String { validate() { this.startsWith("game-") } }
type OptionalGameId extends String { validate() { this == "" || this.startsWith("game-") } }
type MessageId extends String { validate() { this.startsWith("message-") } }
type PublicPlayerId extends String { validate() { this.startsWith("publicPlayer-") } }
type PrivatePlayerId extends String { validate() { this.startsWith("privatePlayer-") } }
type OptionalPlayerId extends String { validate() { this == "" || this.startsWith("publicPlayer-") } }
type GroupId extends String { validate() { this.startsWith("group-") } }
type UserId extends String { validate() { !this.includes("-") } }
type MissionId extends String { validate() { this.startsWith("mission-") } }
type RewardCategoryId extends String { validate() { this.startsWith("rewardCategory-") } }
type GunId extends String { validate() { this.startsWith("gun-") } }
type RewardId extends String { validate() { this.startsWith("reward-") } }
type PrivateLifeId extends String { validate() { this.startsWith("privateLife-") } }
type PublicLifeId extends String { validate() { this.startsWith("publicLife-") } }
type MapId extends String { validate() { this.startsWith("map-") } }
type MarkerId extends String { validate() { this.startsWith("marker-") } }
type QueuedNotificationId extends String { validate() { this.startsWith("queuedNotification-") } }
type NotificationId extends String { validate() { this.startsWith("notification-") } }


/******************************************************************************
 Simple Types That Need Validation
******************************************************************************/
type Allegiance extends String {
  validate() {
    this == 'resistance' || this == 'horde'
  }
}

type OptionalAllegiance extends String {
  validate() {
    this == 'none' || this == 'resistance' || this == 'horde'
  }
}

type Timestamp extends Number {
  validate() {
    this > 1490000000000 // March 2017, milliseconds
  }
}

type OptionalTimestamp extends Number {
  validate() {
    this == 0 || this > 1490000000000 // March 2017, milliseconds
  }
}

type HtmlUrl extends String {
  validate() {
    this.includes("/") && this.includes(".")
  }
}

/******************************************************************************
 Simple Types
******************************************************************************/
type ImageUrl {
  url: HtmlUrl;
}

type Location {
  latitude: Number;
  longitude: Number;
}

type PublicEntity {
  read() { isAuthenticated() }
  write() { isServer() }
  validate() { true; }
}

/******************************************************************************
 Helper Functions Needed By Complex Types
******************************************************************************/

getUserId() {
  ('user-' + auth.uid)
}

getPrivateLifeId() {
  ('privateLife-' + auth.uid)  
}

getCurrentUser() {
  root.users[getUserId()]
}

getCurrentPlayerId(gameId) {
  getCurrentUser().games[gameId].playerId
}

isGroupMember(groupId, gameId) {  
  root.groups[groupId].players[getCurrentPlayerId(gameId)]
}

isAdmin(gameId) {
  root.games[gameId].adminUsers[getUserId()]
}

isAuthenticated() {
  auth != null && auth.uid != null
}

isServer() {
  auth.uid == 'ZiD9uLhDnrLq2n416MjWjn0JOrci6H0oGm7bKyVN' // TODO: place in safe
}

isAuthenticatedMember(groupId, gameId) {
  isAuthenticated() && (
  isServer() ||
  isAdmin(gameId) || 
  isGroupMember(groupId, gameId))
}

/******************************************************************************
 Complex Types
******************************************************************************/

type ChatRoom {
  ChatRoomId: ChatRoomId; // #1
  name: String;
  withAdmins: Boolean;
  gameId: GameId;
  accessGroupId: GroupId;
  messages: Map<MessageId, Message>;

  read() {
    isAuthenticatedMember(this.accessGroupId, this.gameId)
  }

  write() {     isServer()   }

  validate() {
    root.games[this.gameId] &&
    root.groups[this.accessGroupId]
  }
}


type Message {
  MessageId: MessageId; // #1
  message: String;
  playerId: PublicPlayerId;
  time: Timestamp;
  image: ImageUrl;
  location: Location;

  read() {
    isAuthenticatedMember(this.parent().accessGroupId, this.parent().gameId)
  }

  write() {     isServer()   }

  validate() {
    root.chatRooms[this.parent().ChatRoomId]
  }
}

type Game {
  GameId: GameId; // #1
  active: Boolean;
  name: String;
  startTime: OptionalTimestamp;
  endTime: OptionalTimestamp
  registrationEndTime: OptionalTimestamp;
  stunTimer: Number;
  faqHtml: String;
  rulesHtml: String;

  adminUsers: Map<UserId, Boolean>;
  chatRooms: Map<ChatRoomId, Boolean>;
  missions: Map<MissionId, Boolean>;
  guns: Map<GunId, Boolean>;
  players: Map<PublicPlayerId, Boolean>;
  rewardCategories: Map<RewardCategoryId, Boolean>;

  // Everyone can read a game so they can add themselves
  read() {
    isAuthenticated() 
  }

  write() {     isServer()   }
  
  validate() {
    true;
  }
}

type Group {
  GroupId: GroupId; // #1
  allegianceFilter: OptionalAllegiance;
  autoAdd: Boolean;
  autoRemove: Boolean;
  canAddOthers: Boolean;
  canAddSelf: Boolean;
  canRemoveOthers: Boolean;
  canRemoveSelf: Boolean;
  gameId: GameId;
  name: String;
  ownerPlayerId: PublicPlayerId;
  players: Map<PublicPlayerId, Boolean>;

  read() {
    isAuthenticatedMember(key(), this.gameId)
  }
  
  write() {     isServer()   }

  validate() {
    root.games[this.gameId] != null
  }
}


type Gun {
  GunId: GunId; // #1
  playerId: OptionalPlayerId;
  gameId: OptionalGameId;
  label: String;

  read() {
    isAuthenticated()
  }  

  write() {     isServer()   }

  validate() {
    this.label.length > 0
  }
}

type Mission {
  MissionId: MissionId; // #1
  beginTime: Timestamp;
  endTime: Timestamp;
  detailsHtml: String;
  gameId: GameId;
  accessGroupId: GroupId;
  name: String;
  rsvpersGroupId: GroupId;

  read() {
    isAuthenticatedMember(this.accessGroupId, this.gameId)
  }

  write() {     isServer()   }

  validate() {
    root.games[this.gameId] != null
  }
}

type Volunteer {
  admin: Boolean;
  advertising: Boolean;
  android: Boolean;
  chronicler: Boolean;
  cleric: Boolean;
  client: Boolean;
  communications: Boolean;
  ios: Boolean;
  logistics: Boolean;
  moderator: Boolean;
  photographer: Boolean;
  server: Boolean;
  sorcerer: Boolean;
}

type NotificationSettings {
  sound: Boolean;
  vibrate: Boolean;
}

type ChatRoomLookup {
  isVisible: Boolean;
}

type Notification {
  destination: HtmlUrl;
  icon: String;
  message: String;
  previewMessage: String;
  queuedNotificationId: QueuedNotificationId;
  time: OptionalTimestamp
}

type PrivatePlayer {
  PrivatePlayerId: PrivatePlayerId; // #1
  beInPhotos: Boolean;
  canInfect: Boolean;
  gameId: GameId;
  gotEquipment: Boolean;
  needGun: Boolean;
  notes: String;
  notificationSettings: NotificationSettings;
  notifications: Map<NotificationId, Notification>;
  userId: UserId;
  volunteer: Volunteer;
  wantToBeSecretZombie: Boolean;
  chatRoomMemberships: Map<ChatRoomId, ChatRoomLookup>;
  missionMemberships: Map<MissionId, Boolean>;
  associatedMaps: Map<MapId, Map<MarkerId, Boolean>>;

  read() {
    isAuthenticated() && (isServer() ||
    getUserId() == this.userId || isAdmin(this.gameId))
  }

  write() {     isServer()   }

  validate() {
    root.users[this.userId] != null
  }
}


type Reward {
  RewardId: RewardId; // #1
  rewardCategoryId: RewardCategoryId;
  time: Timestamp;
  code: String;
  playerId: OptionalPlayerId;

  read() {
    isAuthenticated()
  }

  write() {     isServer()   }

  validate() { // #2
    this.playerId == "" ||
    root.publicPlayers[this.playerId] != null
  }
}

type PublicPlayer {
  PublicPlayerId: PublicPlayerId; // #1
  isActive: Boolean;
  allegiance: Allegiance;
  claims: Map<RewardId, Reward>;
  name: String;
  number: Number;
  points: Number;
  profileImageUrl: HtmlUrl;
  userId: UserId;
  privatePlayerId: PrivatePlayerId;

  // Public players can be seen by all
  read() {
    isAuthenticated()
  }

  write() {     isServer()   }

  validate() {
    root.users[this.userId] != null
  }
}

type RewardCategory {
  RewardCategoryId: RewardCategoryId; // #1
  badgeImageUrl: HtmlUrl;
  claimed: Number;
  description: String;
  gameId: GameId;
  limitPerPlayer: Number;
  name: String;
  points: Number;
  rewards: Map<RewardId, Boolean>;
  shortName: String;

  read() {
    isAuthenticated()
  }

  write() {     isServer()   }

  validate() {
    root.games[this.gameId] != null
  }
  
}

type Player {
  gameId: GameId;
}

type GamePlayerLookup {
  playerId: PublicPlayerId;
}

type User {
  UserId: UserId; // #1
  a: Boolean;
  publicPlayers: Map<PublicPlayerId, Player>;
  games: Map<GameId, GamePlayerLookup>;

  read() {
    isServer() ||
    key() == getUserId()
  }

  write() {     isServer()   }

  validate() {
    true;
  }
}

type Marker {
  color: String;
  latitude: Number;
  longitude: Number;
  name: String;
  playerId: PublicPlayerId 
}

type MapType {
  MapId: MapId; // #1
  accessGroupId: GroupId;
  gameId: GameId;
  markers: Map<MarkerId, Marker>;
  name: String; 

  read() {
    isAuthenticated()
  }

  write() {     isServer()   }

  validate() {
    true;
  }
}

type QueuedNotification {
  QueuedNotificationId: QueuedNotificationId; // #1
  destintation: HtmlUrl;
  email: Boolean;
  gameId: GameId;
  groupId: GroupId;
  message: String;
  mobile: Boolean;
  previewMessage: String;
  sent: Boolean;
  size: Boolean;
  sound: Boolean;
  vibrate: Boolean;
  
  read() {
    isAuthenticatedMember(this.groupId, this.gameId)
  }

  write() {     isServer()   }

  validate() {
    true;
  }
}

type PrivateLife {
  PrivateLifeId: PrivateLifeId; // #1
  code: String;
  gameId: GameId;
  read() {
    isServer() ||
    key() == getPrivateLifeId()
  }

  write() {     isServer()   }

  validate() {
    true;
  }
}

type PublicLife {
  PublicLifeId: PublicLifeId; // #1
  privateLifeId: PrivateLifeId;
  gameId: GameId;
  time: OptionalTimestamp;
  read() {
    isAuthenticated()
  }

  write() {     isServer()   }

  validate() {
    true;
  }
}


/******************************************************************************
 Possible Paths
******************************************************************************/
/ {
  write() { isServer() }
}

/chatRooms/{ChatRoomId} is ChatRoom;
/chatRooms/ChatRoomId/messages/{MessageId} is Message;
/games/{GameId} is Game;
/games is PublicEntity;
/games/GameId/adminUsers/{UserId} is User;
/groups/{GroupId} is Group;
/groups/GroupId/players/{PlayerId} is PrivatePlayer;
/guns/{GunId} is Gun;
/queuedNotification/{QueuedNotificationId} is QueuedNotification;
/maps/{MapId} is MapType;
/missions/{MissionId} is Mission;
/playersPrivate/{PlayerId} is PrivatePlayer;
/privateLives/{PrivateLifeId} is PrivateLife;
/playersPublic/{PlayerId} is PublicPlayer;
/publicLives/{PublicLifeId} is PublicLife;
/rewardCategories/{RewardCategoryId} is RewardCategory;
/rewards/{RewardId} is RewardCategory;
/users/{UserId} is User;
/users/UserId/players/{PlayerId} is PrivatePlayer;
